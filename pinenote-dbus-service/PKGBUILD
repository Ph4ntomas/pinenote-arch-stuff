# Maintainer: Gabriel Cadet <phantomas@phantomas.xyz>

buildarch=8

pkgbase=pinenote-dbus-service
pkgname=('pinenote-dbus-service' 'pinenote-dbus-service-utils')
pkgver=0.2.5
pkgrel=1
_commit='83ed0186deb3ac908ccb2acc6deb2a8485832791'
arch=('aarch64')
url="https://github.com/PNDeb/pinenote_dbus_service"
license=('Apache-2.0' 'MIT')
depends=('dbus' 'gcc-libs' 'glibc')
makedepends=('cargo')
source=(
     "$pkgbase-$pkgver.${_commit:0:10}.tar.gz::https://github.com/PNDeb/pinenote_dbus_service/archive/${_commit}.tar.gz"
     org.pinenote.ebc.service
     pinenote_dbus.service
)
md5sums=(
    'e55e439b636f3bb2ec14961ee3202672'
    '31bfd6f0706f570d6dce049cb5ef43d1'
    '2c473845cfbe797699115ccf0d45021b'
)

_srcname=pinenote_dbus_service-${_commit}

prepare() {
    cd "${_srcname}"

    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "${_srcname}"

    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

package_pinenote-dbus-service() {
    pkgdesc='PineNote D-Bus Service.
    A simple DBus service that exposes various controls and settings related to the Pine64 Pinenote via system-wide DBus interface.'
    cd "${_srcname}"

    # Install main binary
    install -Dm755 target/release/pinenote_dbus_service -t "${pkgdir}/usr/bin" #main package

    # Install dbus security configuration
    install -Dm644 dbus_security_configuration/pinenote.conf -t "${pkgdir}/usr/share/dbus-1/system.d/"

    # Systemd unit files 
    install -Dm644 ../pinenote_dbus.service -t "${pkgdir}/usr/lib/systemd/system/"
    install -Dm644 ../org.pinenote.ebc.service -t "${pkgdir}/usr/share/dbus-1/system-services/"

    install -Dm644 LICENSE-APACHE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_pinenote-dbus-service-utils() {
    pkgdesc='PineNote D-Bus Service - Utilities'
    depends+=("pinenote-dbus-service=${pkgver}")
    cd "${_srcname}"

    install -Dm755 target/release/pn_enter_writing_mode -t "${pkgdir}/usr/bin/"
    install -Dm755 target/release/pn_quit_writing_mode -t "${pkgdir}/usr/bin/"
    install -Dm755 target/release/pn_trigger_global_refresh -t "${pkgdir}/usr/bin/"

    install -Dm644 LICENSE-APACHE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
